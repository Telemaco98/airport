/* Airplane_i
 * Author: ohara
 * Creation date: 06/11/2019
 */

IMPLEMENTATION Airplane_i
REFINES Airplane

SEES
    Airport_ctx

CONCRETE_VARIABLES
    airplanes_i,  pointer
    
INVARIANT
    pointer : 0..sz_airplanes
    & airplanes_i : 0..sz_airplanes --> AIRPLANE
    & (0..(pointer-1) <| airplanes_i) : 0..(pointer-1) >-> AIRPLANE 
    
    & ran(0..(pointer-1) <| airplanes_i) = ran(airplanes) //invariant de ligação
    
INITIALISATION
    airplanes_i := (0..sz_airplanes) * {plane_dummy};
    pointer := 0
    
OPERATIONS
    add_plane(aa) =
        BEGIN 
            IF pointer < sz_airplanes 
            THEN VAR ii, nsz, bb IN 
                    ii := 0;
                    nsz := pointer;
                    bb := FALSE;
                
                    WHILE ii < pointer
                    DO VAR cc IN
                           cc := airplanes_i(ii);
                           IF cc = aa 
                           THEN bb := TRUE
                           END
                       END;
                       ii := ii + 1
                    INVARIANT ii : 0..pointer
                            & (bb = TRUE <=> #xx.(xx : 0..ii-1 & airplanes_i(xx) = aa))
                    VARIANT nsz - ii
                    END;
                    
                    IF bb = FALSE
                    THEN airplanes_i(pointer) := aa;
                         pointer := pointer + 1
                    END
                END                 
            END
        END;
    
    remove(aa) =
        BEGIN
            IF pointer > 0
            THEN VAR ii, nsz IN
                    ii := 0;
                    nsz := pointer;
                    
                    WHILE ii < pointer
                    DO VAR cc IN 
                           cc := airplanes_i(ii);
                           IF cc = aa
                           THEN airplanes_i(ii) := airplanes_i(pointer -1);
                                airplanes_i(pointer - 1) := plane_dummy;
                                pointer := pointer - 1
                           END
                       END;
                       ii := ii + 1
                    INVARIANT ii : NAT
                    VARIANT nsz - ii
                    END
                 END
            END
        END;
        
    bb <-- has_airplane (aa) =
        BEGIN
            VAR ii, nsz IN 
                ii := 0;
                nsz := pointer;
                bb := FALSE;
                WHILE ii < pointer
                DO VAR cc IN
                       cc := airplanes_i(ii);
                       IF cc = aa 
                       THEN bb := TRUE
                       END
                   END;
                   ii := ii + 1
                INVARIANT ii : 0..pointer
                        & (bb = TRUE <=> #xx.(xx : 0..ii-1 & airplanes_i(xx) = aa))
                VARIANT nsz - ii
                END
            END
        END    
END